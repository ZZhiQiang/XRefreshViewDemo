apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.wisdom.zj'                                  //***com.zzq.net
project.archivesBaseName ='XRefreshView'                //***com.zzq.net:TextLib
version = '1.0.1'                                       //***com.zzq.net:TextLib:1.0.1

def siteUrl = 'https://github.com/ZZhiQiang/JCenterDemo'
def gitUrl = 'https://github.com/ZZhiQiang/JCenterDemo.git'
def issuesUrl='https://github.com/ZZhiQiang/JCenterDemo/issues'


install {
    repositories.mavenInstaller {
        pom {
            project {

                packaging 'aar'
                name 'this is project'     // 项目描述
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'

                    }
                }
                developers {
                    developer {
                        id 'zhuzhiqiang'                  // 开发者信息
                        name 'zhuzhiqnag'                  // 开发者信息
                        email '1316995875@qq.com'          // 开发者信息
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false// 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title 'XRefreshView For Android'   // 文档标题
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// 生成jar包
task releaseJar(type: Copy) {
    from( 'build/intermediates/bundles/release')
    into( '../jar')
    include('classes.jar')
    rename('classes.jar', 'xrefreshview-' + version + '.jar')
}





Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']


    pkg {
        repo = "maven"                  //库名 ***bintray下的建的库名为maven  最好固定写死maven
        name = "XRefreshView"           //子库 ***（package）bintray下的建的包名 和module名保持一致
        desc = "this is project"        //描述

        websiteUrl = siteUrl
        issueTrackerUrl =issuesUrl
        vcsUrl = gitUrl

        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true

    }
}

//工程目录下 loacl.properties配置
//bintray.user=1316995875
//bintray.apikey=7f792b911cd03b6d04d4e5fb7791cb1a05020e80
